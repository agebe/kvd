plugins {
  id 'com.palantir.git-version' version '0.12.3'
  id "name.remal.check-dependency-updates" version "1.1.6"
  id("maven-publish")
}

allprojects {
  project.version = '0.0.2'
}

subprojects {

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'eclipse'
  apply plugin: "name.remal.check-dependency-updates"
  apply plugin: "maven-publish"

  compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }

  repositories {
    jcenter()
  }

  publishing {
    repositories {
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/agebe/kvd")
        credentials {
          username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
          password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
      }
    }
    publications {
      gpr(MavenPublication) {
        from(components.java)
      }
    }
  }

  dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'
  }

  jar {
    manifest {
      attributes(
        'Implementation-Version': project.version,
        'Implementation-Title': project.name,
        'git-version': gitVersion())
    }
  }

}
